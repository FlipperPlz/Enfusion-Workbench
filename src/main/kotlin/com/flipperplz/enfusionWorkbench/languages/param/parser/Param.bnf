{
    parserClass="com.flipperplz.enfusionWorkbench.languages.param.parser.ParamParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Param"
    psiImplClassSuffix="impl"
    psiPackage="com.flipperplz.enfusionWorkbench.languages.param.psi"

    elementTypeHolderClass="com.flipperplz.enfusionWorkbench.languages.param.psi.ParamTypes"
    elementTypeClass="com.flipperplz.enfusionWorkbench.languages.param.psi.ParamElementType"
    tokenTypeClass="com.flipperplz.enfusionWorkbench.languages.param.psi.ParamTokenType"

    tokens = [
        ABS_IDENTIFIER   =  'regexp:[a-zA-Z_][a-zA-Z0-9_]*';
        ABS_STRING       =  'regexp:"(""|.)*"';
        ABS_NUMERIC      =  'regexp:(-?[0-9]+(.[0-9]+)?([eE][-+]?[0-9]+)?|0x[a-fA-F0-9]+)';
        ABS_WHITE_SPACE      = 'regexp:[\t\s]+';
    ]
}

paramTree ::= paramStatement* paramEnumDeclaration?

paramClassDeclaration ::= 'class' ABS_IDENTIFIER paramRegularClass? ';' {pin=2}
paramAssignmentStatement ::= ABS_IDENTIFIER (paramArrayAssignment | paramValueAssignment) ';' {pin=1};
paramDeleteStatement ::= 'delete' ABS_IDENTIFIER ';' {pin=1}
paramLiteral ::= ABS_STRING | ABS_NUMERIC;
paramLiteralArray ::= '{' paramArrayElement (',' (paramArrayElement | &'}'))* '}' {pin(".*")=1};

enumValue ::= ABS_IDENTIFIER ('=' ABS_NUMERIC)? {pin=1}

private paramEnumDeclaration ::= 'enum' '{' enumValue* '}' ';' {pin=2}
private paramArrayAssignment ::= '[' ']' (
    '='  |
    '+=' |
    '-='
) paramLiteralArray;
private paramValueAssignment ::= '=' paramLiteral;


private paramArrayElement ::= paramLiteral | paramLiteralArray;

private paramStatement ::=
    paramClassDeclaration | paramDeleteStatement | paramAssignmentStatement;

private paramRegularClass ::= (':' ABS_IDENTIFIER)? '{' paramStatement* '}' {pin=2}




