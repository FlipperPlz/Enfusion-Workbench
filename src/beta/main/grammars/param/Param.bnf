{
    parserClass="com.flipperplz.enfusionWorkbench.languages.param.parser.ParamParser"

    extends="com.flipperplz.enfusionWorkbench.languages.param.psi.impl.ParamCompositeElementImpl"
    implements="com.flipperplz.enfusionWorkbench.languages.param.psi.ast.ParamCompositeElement"

    psiClassPrefix="GeneratedParam"
    psiImplClassSuffix="impl"
    psiPackage="com.flipperplz.enfusionWorkbench.languages.param.psi"
    psiImplPackage="com.flipperplz.enfusionWorkbench.languages.param.psi.impl"

    elementTypeHolderClass="com.flipperplz.enfusionWorkbench.languages.param.psi.ParamTypes"
    elementTypeClass="com.flipperplz.enfusionWorkbench.languages.param.psi.required.ParamElementType"
    tokenTypeClass="com.flipperplz.enfusionWorkbench.languages.param.psi.required.ParamTokenType"

    tokens = [
        SINGLE_LINE_COMMENT      = 'regexp://.*';
        EMPTY_DELIMITED_COMMENT  = 'regexp:/\*\*?/';
        DELIMITED_COMMENT        = 'regexp:/\*([^*]|[\r\n]|(\*+([^*/]|[\r\n])))*\*+/'
        ABS_IDENTIFIER   =  'regexp:[a-zA-Z_][a-zA-Z0-9_]*';
        ABS_STRING       =  'regexp:"(""|.)*"';
        ABS_NUMERIC      =  'regexp:(-?[0-9]+(.[0-9]+)?([eE][-+]?[0-9]+)?|0x[a-fA-F0-9]+)';
        ABS_WHITE_SPACE      = 'regexp:[\t\s]+';
    ]
    generate=[tokenAccessors="yes"]

    extends(".*Statement")="com.flipperplz.enfusionWorkbench.languages.param.psi.impl.ParamStatementImpl"
    implements(".*Statement")="com.flipperplz.enfusionWorkbench.languages.param.psi.ext.ParamStatement"

}

file ::= statement* enumDeclaration?
{
    pin=1
}

classDeclaration ::= 'class' identifier regularClassDecl? ';'
{
    pin=2
    extends="com.flipperplz.enfusionWorkbench.languages.param.psi.impl.ParamStatementImpl"
    implements="com.flipperplz.enfusionWorkbench.languages.param.psi.ext.ParamClass"
    mixin="com.flipperplz.enfusionWorkbench.languages.param.psi.mixins.ParamPsiClassMixin"
}
assignmentStatement ::= identifier (arrayAssignment | valueAssignment) ';'
{
    pin=1
};
deleteStatement ::= 'delete' identifier ';'
{
    pin=1
}
private literal ::= paramString | paramNumeric //| '__FILE__' | '__LINE__'

paramNumeric ::= ABS_NUMERIC
{
   implements="com.flipperplz.enfusionWorkbench.languages.param.psi.ext.ParamNumber"
   mixin="com.flipperplz.enfusionWorkbench.languages.param.psi.mixins.ParamPsiNumericMixin"
}

paramString ::= ABS_STRING
{
   implements="com.flipperplz.enfusionWorkbench.languages.param.psi.ext.ParamString"
   mixin="com.flipperplz.enfusionWorkbench.languages.param.psi.mixins.ParamPsiStringMixin"
};

literalArray ::= '{' arrayElement (',' (arrayElement | &'}'))* '}'
{
    pin(".*")=1
    implements="com.flipperplz.enfusionWorkbench.languages.param.psi.ext.ParamArray"
    mixin="com.flipperplz.enfusionWorkbench.languages.param.psi.mixins.ParamPsiArrayMixin"
};

private statement ::=
    classDeclaration | deleteStatement | assignmentStatement
{
    recoverWhile="statementRecover"
};

enumDeclaration ::= 'enum' '{' enumValue* '}' ';'
{
    pin=2
}

private arrayAssignment ::= '[' ']' (
        '='  |
        '+=' |
        '-='
    ) literalArray
{
    pin=3
};

private valueAssignment ::= '=' literal { pin=1 };

private enumValue ::= identifier ('=' ABS_NUMERIC)?
{
    pin=1
}

private arrayElement ::= literal | literalArray
{
    implements="com.flipperplz.enfusionWorkbench.languages.param.psi.ext.ParamArrayElement"
}

private statementRecover ::= !(';' | identifier)

inner regularClassDecl ::= (':' identifier)? '{' statement* '}'
{
    pin=2
}

identifier ::= ABS_IDENTIFIER
{
    mixin="com.flipperplz.enfusionWorkbench.languages.param.psi.mixins.ParamPsiIdentifierMixin"
    implements="com.flipperplz.enfusionWorkbench.languages.param.psi.ext.ParamIdentifier"
}





